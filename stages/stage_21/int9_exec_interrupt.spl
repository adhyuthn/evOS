breakpoint;
// MODE FLAG
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = INT_EXEC;

alias userSP R0;
userSP = SP;

// Store SP in UPTR
[PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 13] = userSP;

// Switcht to the kernel stack of the process
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 11] * 512 - 1;

alias fileName R1;
alias physicalAddrRetVal R2;
alias inode R3;

fileName = [([PTBR + 2 * ((userSP - 4)/ 512)] * 512) + (userSP - 4) % 512];
physicalAddrRetVal = ([PTBR + 2 * ((userSP - 1) / 512)] * 512) + ((userSP - 1) % 512);

[physicalAddrRetVal] = -1;

alias counter R4;
counter = 0;
// file existence and EXEC check
while (counter < MAX_FILE_NUM) do 
    if ([INODE_TABLE + counter*16 + 1] == fileName) then
        if ([INODE_TABLE + counter*16] == EXEC ) then
            inode = counter;
            [physicalAddrRetVal] = 0;
        endif;
        break;
    endif;
    counter = counter + 1;
endwhile;

if ([physicalAddrRetVal] == -1) then 
    [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;
    SP = userSP;
    ireturn;
endif;

//Call EXIT_PROCESS
multipush(R0, R1, R2, R3);
R1 = EXIT_PROCESS;
R2 = [SYSTEM_STATUS_TABLE + 1];
call MOD_1;
multipop(R0, R1, R2, R3);

// Reclaim User Page
alias userAreaPageNum R5;
userAreaPageNum = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 11];
[MEMORY_FREE_LIST + userAreaPageNum] = [MEMORY_FREE_LIST + userAreaPageNum] + 1;
[SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] - 1;
// Init Per-Process Table
counter = 0;
while (counter < 8) do 
    [userAreaPageNum * 512 + RESOURCE_TABLE_OFFSET + counter * 2] = -1;
    counter = counter + 1;
endwhile;

// SP = KPTR of new process
SP = ([PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 11]) * 512 - 1;

// Update STATE and INODE 
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 4] = RUNNING;
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 7] = inode;

// Allocate Pages

// Library
[PTBR+0] = 63;
[PTBR+1] = "0100";
[PTBR+2] = 64;
[PTBR+3] = "0100";

// Heap (Don't Allocate)
[PTBR+4] = -1;
[PTBR+5] = "0000";
[PTBR+6] = -1;
[PTBR+7] = "0000";

// Stack
multipush(R0, R1, R2, R3);
R1 = GET_FREE_PAGE;
call MOD_2;
[PTBR+16] = R0;
[PTBR+17] = "0110";
R1 = GET_FREE_PAGE;
call MOD_2;
[PTBR+18] = R0;
[PTBR+19] = "0110";
multipop(R0, R1, R2, R3);

// Code (1 page enef)
if ([INODE_TABLE + inode * 16 + 8] != -1) then
    multipush(R0, R1, R2, R3, R4); 
    R1 = GET_CODE_PAGE; 
    R2 = [INODE_TABLE + inode * 16 + 8];
    call MOD_2;
    [PTBR+8] = R0;
    [PTBR+9] = "0100";
    multipop(R0, R1, R2, R3, R4);
else 
    [PTBR+8] = -1;
    [PTBR+9] = "0000";
endif;

[PTBR+10] = -1;
[PTBR+11] = "0000";
[PTBR+12] = -1;
[PTBR+13] = "0000";
[PTBR+14] = -1;
[PTBR+15] = "0000";

// Disk Map Table
counter = 0;
while(counter < 9) do
    [DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE + 1] * 10 + 1 + counter] = -1;
    counter = counter + 1;
endwhile;
counter = 0;
while(counter < 4) do
    [DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE + 1] * 10 + 4 + counter] = [INODE_TABLE + inode * 16 + 8 + counter];
    counter = counter + 1;
endwhile;

// Update IP and SP
SP = 8 * 512;
[[PTBR+16] * 512] = [[PTBR+8] * 512 + 1];

// MODE
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;
ireturn;
